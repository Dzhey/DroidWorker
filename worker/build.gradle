apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 3
        versionName "0.2.2b"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding = 'utf-8'
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        quiet false
        abortOnError true
        ignoreWarnings false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${defaultConfig.versionName}-${variant.name}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

sourceSets {
    unitTest {
        java.srcDir file('test/java')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // JUnit, Mockito and PowerMock
    testCompile 'junit:junit:4.12'
    testCompile ("org.mockito:mockito-core:1.9.5") {
        exclude group: 'org.hamcrest'
    }
    testCompile ('org.powermock:powermock-module-junit4:1.6.2') {
        exclude group: 'org.hamcrest'
    }
    testCompile ('org.powermock:powermock-api-mockito:1.6.2') {
        exclude group: 'org.hamcrest'
    }
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}
